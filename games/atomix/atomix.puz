title Atomix demake
author Recher
homepage recher.wordpress.com

(HOW TO MAKE LEVELS)

(Condition 1 : a level must have two zones : )
( - the "arena" zone, which contains the player and the atoms to move)
( - the "model" zone, which shows the final pattern to make with the atoms)
(The game automatically detects the two zones from the Player initial position. The "arena" zone is where the Player is.)
(The arena zone and the model zone MUST be separated by a vertical or horizontal line of wall that passes through the whole level.)
()
(If this condition is not fullfilled, an error message will be written at the beginning of the level, and the Player will disappear)

(Condition 2 : a level must have at least one unique element)
(An "element" = the atom and the links on one single square)
(In order to perform the pattern comparrison between the model and the arena, the game must have a unique element from where to start.)
(This element is automatically detected at the beginning of the level. But there MUST have one at least.)
(Example of a model with no unique element)
(         )
(  H--H   )
(         )
(         )
(  H--H   )
(         )
(There are 4 elements : two of them are consisted of a Hydrogen with a link on the left,)
(the two others are a Hydrogen with a link on the right. There is no unique element. This model is not usable.)
(Example of a model with many unique element)
(         )
(  H--H   )
(  |      )
(  |      )
(  H--H   )
(         )
(There are 4 elements : two of them are consisted of a Hydrogen with a link on the left, one is a Hydrogen with a link on the right and a link to the up, one last is a Hydrogen with a link on the right and a link to the bottom)
(This model has 2 unique elements. It is usable. It would still be usable even with only one unique element.)
()
(If this condition is not fullfilled, an error message will be written at the beginning of the level, and the Player will disappear)

(Info : no check is made on link coherency)
(Your model can have something like this :)
( H- H )
(The First hydrogen has a link to the right, so the second Hydrogen is supposed to have a link to the left. But he has not.)

(Info : no check is made on level feasability)
(Your model can have a Hydrogen atom, without having one in the arena.)
(Your level can not be won. But the game does not check this case and does not send message about it.)

verbose_logging
again_interval 0.6

run_rules_on_level_start

========
OBJECTS
========

Background
black

WallBrick
#494949 #707070 #9E9E9E #D3D3D3
23332
12320
11200
12120
21112

ModelZone
(debug = darkgray. release = transparent)
transparent
(darkgray)

ArenaZone
#392900

Hydrogen
transparent #4460FF #2646FF #0026FF #001ECC #0019AA
00000
01230
02340
03450
00000

Carbon
transparent gray
00000
01110
01110
01110
00000

Oxygen
transparent red
00000
01110
01110
01110
00000

Nitrogen
transparent green
00000
01110
01110
01110
00000

Sulfur
transparent orange
00000
01110
01110
01110
00000

Chlorine
transparent #D0D0D0
00000
01110
01110
01110
00000

Phosphore
transparent purple
00000
01110
01110
01110
00000

Cristal
transparent lightblue
00000
01110
01110
01110
00000

(Useful to have elements with link only)
(Putting an EmptyAtom with no link in a level would not be nice.)
EmptyAtom
transparent

Fluorine
transparent yellow
00000
01110
01110
01110
00000


(TODO : draw prettier atoms)

AtomTagged
transparent

AtomCheckedOK
transparent

MovingAtom
transparent #FF6A00 #FF8A38 #FFA566
23212
10003
20002
30001
21232

SelectedAtom
transparent yellow
10101
00000
10001
00000
10101

Player
transparent #FF0000
01010
10001
00000
10001
01010

UniqueAtomStartCheck
(debug = red point. release = transparent)
transparent
(transparent red)
(00000)
(00000)
(00100)
(00000)
(00000)

CopyToRight
transparent

CopyToUp
transparent

CopyToDown
transparent

CopyToLeft
transparent

CheckZone
(debug = purple. release = transparent)
transparent
(purple)

LinkLeftSimple
transparent #eeeeee
00000
00000
11100
00000
00000

LinkLeftDouble
transparent #eeeeee
00000
11100
00000
11100
00000

LinkLeftTriple
transparent #eeeeee
00000
11100
11100
11100
00000

LinkLeftTagged
transparent

LinkLeftCheckedOK
transparent

LinkRightSimple
transparent #eeeeee
00000
00000
00111
00000
00000

LinkRightDouble
transparent #eeeeee
00000
00111
00000
00111
00000

LinkRightTriple
transparent #eeeeee
00000
00111
00111
00111
00000

LinkRightTagged
transparent

LinkRightCheckedOK
transparent

LinkUpSimple
transparent #eeeeee
00100
00100
00100
00000
00000

LinkUpDouble
transparent #eeeeee
01010
01010
01010
00000
00000

LinkUpTriple
transparent #eeeeee
01110
01110
01110
00000
00000

LinkUpTagged
transparent

LinkUpCheckedOK
transparent

LinkDownSimple
transparent #eeeeee
00000
00000
00100
00100
00100

LinkDownDouble
transparent #eeeeee
00000
00000
01010
01010
01010

LinkDownTriple
transparent #eeeeee
00000
00000
01110
01110
01110

LinkDownTagged
transparent

LinkDownCheckedOK
transparent

LinkUpRightSimple
transparent #eeeeee
00001
00010
00100
00000
00000

LinkUpRightTagged
transparent

LinkUpRightCheckedOK
transparent

LinkDownRightSimple
transparent #eeeeee
00000
00000
00100
00010
00001

LinkDownRightTagged
transparent

LinkDownRightCheckedOK
transparent

LinkDownLeftSimple
transparent #eeeeee
00000
00000
00100
01000
10000

LinkDownLeftTagged
transparent

LinkDownLeftCheckedOK
transparent

LinkUpLeftSimple
transparent #eeeeee
10000
01000
00100
00000
00000

LinkUpLeftTagged
transparent

LinkUpLeftCheckedOK
transparent


GlobalTags
(debug = #FF00FF. release = transparent)
transparent
(#FF00FF)


GlobalTagFoundUniqueAtom
transparent

GlobalTagSearchUniqueFail
transparent

GlobalTagLoopIteration
transparent

GlobalTagRecalculateCheckZone
transparent

GlobalTagCheckZoneUncomplete
transparent

GlobalTagCheckFailed
transparent

LoopIterationToDo
transparent

LoopIterationCurrent
transparent

PlayerTagMustDoInit
transparent

PropagateVertical
transparent

PropagateHorizontal
transparent

CurrentCheckedOK
(debug = pink. release = transparent)
transparent
(pink)

PropagateDiagonalHalfWay
transparent

GlobalTagBorderModelMade
transparent

GlobalTagSlideLeft
transparent

GlobalTagSlideRight
transparent

GlobalTagSlideUp
transparent

GlobalTagSlideDown
transparent

GlobalTagMustCheckPattern
transparent

GlobalTagModelZoneExists
transparent

GlobalTagWon
transparent

WinStar0
transparent

(TODO : prettier Winstars)
WinStar1
transparent lightblue
00000
00100
01110
00100
00000

WinStar2
transparent lightblue
00100
01110
11111
01110
00100

WinStar3
transparent lightblue
00000
00100
01110
00100
00000

WinStar4
transparent

BorderModelLeft
transparent white gray
00012
00012
00012
00012
00012

BorderModelRight
transparent white gray
12000
12000
12000
12000
12000

BorderModelUp
transparent white gray
00000
00000
00000
11111
22222

BorderModelDown
transparent white gray
11111
22222
00000
00000
00000

BorderModelUpRight
transparent white gray #cccccc
00000
00000
00000
13000
22000

BorderModelDownRight
transparent white gray
12000
22000
00000
00000
00000

BorderModelDownLeft
transparent white gray #cccccc
00012
00032
00000
00000
00000

BorderModelUpLeft
transparent white gray
00000
00000
00000
00011
00012


=======
LEGEND
=======

. = Background
# = WallBrick
* = Player and PlayerTagMustDoInit

A = Hydrogen and LinkLeftSimple
B = Hydrogen and LinkRightSimple
C = Hydrogen and LinkUpSimple
D = Hydrogen and LinkDownSimple
E = Hydrogen and LinkUpLeftSimple
F = Oxygen and LinkLeftSimple and LinkDownRightSimple
H = Hydrogen
O = Oxygen
Z = Carbon and LinkLeftSimple and LinkRightSimple
0 = Hydrogen
1 = Carbon
2 = Oxygen
3 = Nitrogen
4 = Sulfur
5 = Chlorine
6 = Phosphore
7 = Cristal
8 = EmptyAtom and LinkUpSimple and LinkDownSimple
9 = Fluorine

Wall = WallBrick
Atom = Hydrogen or Carbon or Oxygen or Nitrogen or Sulfur or Chlorine or Phosphore or Cristal or EmptyAtom or Fluorine

LinkLeft = LinkLeftSimple or LinkLeftDouble or LinkLeftTriple
LinkRight = LinkRightSimple or LinkRightDouble or LinkRightTriple
LinkUp = LinkUpSimple or LinkUpDouble or LinkUpTriple
LinkDown = LinkDownSimple or LinkDownDouble or LinkDownTriple
LinkUpRight = LinkUpRightSimple
LinkDownRight = LinkDownRightSimple
LinkDownLeft = LinkDownLeftSimple
LinkUpLeft = LinkUpLeftSimple
Link = LinkLeft or LinkRight or LinkUp or LinkDown or LinkUpRight or LinkDownRight or LinkDownLeft or LinkUpLeft

Obstacle = Wall or Atom or Link

GlobalTagSlide = GlobalTagSlideLeft or GlobalTagSlideRight or GlobalTagSlideUp or GlobalTagSlideDown

LinkTagged = LinkLeftTagged or LinkRightTagged or LinkUpTagged or LinkDownTagged or LinkUpRightTagged or LinkDownRightTagged or LinkDownLeftTagged or LinkUpLeftTagged

WinStar = WinStar0 or WinStar1 or WinStar2 or WinStar3 or WinStar4

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background

ModelZone
ArenaZone

CheckZone

CurrentCheckedOK

LinkLeftSimple, LinkLeftDouble, LinkLeftTriple
LinkLeftTagged
LinkLeftCheckedOK
LinkRightSimple, LinkRightDouble, LinkRightTriple
LinkRightTagged
LinkRightCheckedOK
LinkUpSimple, LinkUpDouble, LinkUpTriple
LinkUpTagged
LinkUpCheckedOK
LinkDownSimple, LinkDownDouble, LinkDownTriple
LinkDownTagged
LinkDownCheckedOK

LinkUpRightSimple
LinkUpRightTagged
LinkUpRightCheckedOK
LinkDownRightSimple
LinkDownRightTagged
LinkDownRightCheckedOK
LinkDownLeftSimple
LinkDownLeftTagged
LinkDownLeftCheckedOK
LinkUpLeftSimple
LinkUpLeftTagged
LinkUpLeftCheckedOK

Wall, Atom, WinStar
AtomTagged
AtomCheckedOK

Player
SelectedAtom
MovingAtom

UniqueAtomStartCheck

CopyToRight
CopyToUp
CopyToDown
CopyToLeft

GlobalTags
GlobalTagBorderModelMade
GlobalTagFoundUniqueAtom
GlobalTagSearchUniqueFail
GlobalTagLoopIteration
GlobalTagRecalculateCheckZone
GlobalTagCheckZoneUncomplete
GlobalTagCheckFailed
GlobalTagSlideLeft, GlobalTagSlideRight, GlobalTagSlideUp, GlobalTagSlideDown
GlobalTagMustCheckPattern
GlobalTagModelZoneExists
GlobalTagWon

LoopIterationToDo
LoopIterationCurrent

PlayerTagMustDoInit

PropagateVertical
PropagateHorizontal
PropagateDiagonalHalfWay

BorderModelLeft, BorderModelRight, BorderModelUp, BorderModelDown, BorderModelUpRight, BorderModelDownRight, BorderModelDownLeft, BorderModelUpLeft


======
RULES
======


( --- Detects the ArenaZone ---)

( Global tags does not exist at the beginning of the game,)
(so we use a global tag on the player.)

[ Player PlayerTagMustDoInit ] -> [ Player PlayerTagMustDoInit ArenaZone ]

(propagates ArenaZone, starting from player)
(Propagation can be made diagonally. Like in the first level of Draknek)
startLoop
    [ Player PlayerTagMustDoInit ][ ArenaZone | no Wall ] -> [ Player PlayerTagMustDoInit ][ ArenaZone | ArenaZone ]
    VERTICAL [ Player PlayerTagMustDoInit ][ ArenaZone | ] -> [ Player PlayerTagMustDoInit ][ ArenaZone | PropagateDiagonalHalfWay ]
    HORIZONTAL [ Player PlayerTagMustDoInit ][ PropagateDiagonalHalfWay | no Wall ] -> [ Player PlayerTagMustDoInit ][ PropagateDiagonalHalfWay | ArenaZone ]
endLoop

[ Player PlayerTagMustDoInit ][ PropagateDiagonalHalfWay ] -> [ Player PlayerTagMustDoInit ][ ]

(The ArenaZone is now determined in the level.)
(It is consisted of one block of adjacent tiles. (possibly "diagonnaly adjacent").)

(Extends the ArenaZone to its bounding rectangle, in order to simplify further processings :)
( - The CheckZone initialization and the Pattern Check)
( - The Player will be able to move in the whole rectangle, allowing him to do "shortcuts" when not carrying atoms.)
(To extend the zone, we draw a vertical line and a horizontal line passing through the whole level, at each existing ArenaZone tile)
(Every tile where a vertical line crosses a horizontal line is considered an ArenaZone tile. (It may already is))
(This algorithm works only if the starting shape is consisted of [possibly adjacent] tiles, which is the case here.)
[ Player PlayerTagMustDoInit ][ ArenaZone ] -> [ Player PlayerTagMustDoInit ][ ArenaZone PropagateVertical PropagateHorizontal ]
(draws lines)
VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
(adds an ArenaZone everywhere two lines cross)
[ PropagateHorizontal PropagateVertical ] -> [ ArenaZone ]
(cleans the drawn lines)
[ PropagateVertical ] -> [ ]
[ PropagateHorizontal ] -> [ ]


( --- Detects the ModelZone ---)

[ Player PlayerTagMustDoInit ][ Atom no ArenaZone ] -> [ Player PlayerTagMustDoInit ][ Atom ModelZone ]
[ Player PlayerTagMustDoInit ][ Link no ArenaZone ] -> [ Player PlayerTagMustDoInit ][ Link ModelZone ]

(Extends the ModelZone to its bounding rectangle, same algorithm as the ArenaZone extension.)
[ Player PlayerTagMustDoInit ][ ModelZone ] -> [ Player PlayerTagMustDoInit ][ ModelZone PropagateVertical PropagateHorizontal ]
VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
[ PropagateHorizontal PropagateVertical ] -> [ ModelZone ]
[ PropagateVertical ] -> [ ]
[ PropagateHorizontal ] -> [ ]
(For the ModelZone extension, one more step is needed.)
(The atoms of the model may be scattered through the level. And the initial ModelZone may not be consisted of one block of adjacent tiles)
(The previous algorithm may have left whole lines (vertical or horizontal) of not-ModelZone in the middle of the bounding rect).
(So, the gaps between the pieces of bounding rect must be filled)
[ Player PlayerTagMustDoInit ][ ModelZone |  | ... | ModelZone ] -> [ Player PlayerTagMustDoInit ][ ModelZone | ModelZone | ... | ModelZone ]

(Raises exception if the two zones overlaps)
[ Player PlayerTagMustDoInit ][ ArenaZone ModelZone ] -> [ ][ ArenaZone ModelZone ] message ERROR : zone overlapping. The model zone and the arena zone must be separated by a horizontal or vertical line of wall passing through the whole level


(--- Puts the GlobalTag anywhere in a "nothing-zone" --- )

(Adds also the global tag to recalculate the CheckZone a first time, and to do a first pattern check, just for fun.)
[ Player PlayerTagMustDoInit ][no ArenaZone no ModelZone ] -> [ Player ][ GlobalTags GlobalTagRecalculateCheckZone GlobalTagMustCheckPattern ]

(Raises exception if the global tag was not created.)
(It never happens when there is a line of wall, because this line is not a model zone nor an arean zone.)
[ Player PlayerTagMustDoInit ] -> [ ] message ERROR : no line of wall. The model zone and the arena zone must be separated by a horizontal or vertical line of wall passing through the whole level

(Raises exception if there is no ModelZone)
[ GlobalTags ][ ModelZone ] -> [ GlobalTags GlobalTagModelZoneExists ][ ModelZone ]
[ GlobalTags no GlobalTagModelZoneExists ][ Player ] -> [ GlobalTags ][ ] message ERROR : no model zone. The model zone and the arena zone must be separated by a horizontal or vertical line of wall passing through the whole level


( --- Adds Borders around the ModelZone --- )

LEFT [ GlobalTags no GlobalTagBorderModelMade ][ ModelZone | no ModelZone ] -> [ GlobalTags ][ ModelZone | BorderModelLeft ]
RIGHT [ GlobalTags no GlobalTagBorderModelMade ][ ModelZone | no ModelZone ] -> [ GlobalTags ][ ModelZone | BorderModelRight ]
UP [ GlobalTags no GlobalTagBorderModelMade ][ ModelZone | no ModelZone ] -> [ GlobalTags ][ ModelZone | BorderModelUp ]
DOWN [ GlobalTags no GlobalTagBorderModelMade ][ ModelZone | no ModelZone ] -> [ GlobalTags ][ ModelZone | BorderModelDown ]

RIGHT [ GlobalTags no GlobalTagBorderModelMade ][ no BorderModelUp | BorderModelUp | ... | BorderModelUp | no BorderModelUp ] -> [ GlobalTags ][ BorderModelUpLeft | BorderModelUp | ... | BorderModelUp | BorderModelUpRight ]
RIGHT [ GlobalTags no GlobalTagBorderModelMade ][ no BorderModelDown | BorderModelDown | ... | BorderModelDown | no BorderModelDown ] -> [ GlobalTags ][ BorderModelDownLeft | BorderModelDown | ... | BorderModelDown | BorderModelDownRight ]

[ GlobalTags ] -> [ GlobalTags GlobalTagBorderModelMade ]


( --- Searches for a unique atom in the ModelZone --- )

(Initiate a loop on all the Atoms and Link of the ModelZone)
[ GlobalTags no GlobalTagFoundUniqueAtom ][ ModelZone Atom ] -> [ GlobalTags ][ ModelZone Atom LoopIterationToDo ]
[ GlobalTags no GlobalTagFoundUniqueAtom ][ ModelZone Link ] -> [ GlobalTags ][ ModelZone Link LoopIterationToDo ]

(Starts one loop iteration, while no unique atom is found.)
startLoop

    [ GlobalTags no GlobalTagFoundUniqueAtom no GlobalTagLoopIteration ][ LoopIterationToDo ] -> [ GlobalTags GlobalTagLoopIteration ][ LoopIterationCurrent ]

    (Tags all the atoms that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no Atom' case))
    [ LoopIterationCurrent Hydrogen ][ Hydrogen ] -> [ LoopIterationCurrent Hydrogen ][ Hydrogen AtomTagged ]
    [ LoopIterationCurrent Carbon ][ Carbon ] -> [ LoopIterationCurrent Carbon ][ Carbon AtomTagged ]
    [ LoopIterationCurrent Oxygen ][ Oxygen ] -> [ LoopIterationCurrent Oxygen ][ Oxygen AtomTagged ]
    [ LoopIterationCurrent Nitrogen ][ Nitrogen ] -> [ LoopIterationCurrent Nitrogen ][ Nitrogen AtomTagged ]
    [ LoopIterationCurrent Sulfur ][ Sulfur ] -> [ LoopIterationCurrent Sulfur ][ Sulfur AtomTagged ]
    [ LoopIterationCurrent Chlorine ][ Chlorine ] -> [ LoopIterationCurrent Chlorine ][ Chlorine AtomTagged ]
    [ LoopIterationCurrent Phosphore ][ Phosphore ] -> [ LoopIterationCurrent Phosphore ][ Phosphore AtomTagged ]
    [ LoopIterationCurrent Cristal ][ Cristal ] -> [ LoopIterationCurrent Cristal ][ Cristal AtomTagged ]
    [ LoopIterationCurrent EmptyAtom ][ EmptyAtom ] -> [ LoopIterationCurrent EmptyAtom ][ EmptyAtom AtomTagged ]
    [ LoopIterationCurrent Fluorine ][ Fluorine ] -> [ LoopIterationCurrent Fluorine ][ Fluorine AtomTagged ]
    [ LoopIterationCurrent no Atom ][ no Atom ] -> [ LoopIterationCurrent no Atom ][ no Atom AtomTagged ]

    (Tags all the LinkLeft that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no LinkLeft' case))
    [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple ] -> [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple LinkLeftTagged ]
    [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble ] -> [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble LinkLeftTagged ]
    [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple ] -> [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple LinkLeftTagged ]
    [ LoopIterationCurrent no LinkLeft ][ no LinkLeft ] -> [ LoopIterationCurrent no LinkLeft ][ no LinkLeft LinkLeftTagged ]

    (Tags all the LinkRight, etc.)
    [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple ] -> [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple LinkRightTagged ]
    [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble ] -> [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble LinkRightTagged ]
    [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple ] -> [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple LinkRightTagged ]
    [ LoopIterationCurrent no LinkRight ][ no LinkRight ] -> [ LoopIterationCurrent no LinkRight ][ no LinkRight LinkRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple ] -> [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple LinkUpTagged ]
    [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble ] -> [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble LinkUpTagged ]
    [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple ] -> [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple LinkUpTagged ]
    [ LoopIterationCurrent no LinkUp ][ no LinkUp ] -> [ LoopIterationCurrent no LinkUp ][ no LinkUp LinkUpTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple ] -> [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple LinkDownTagged ]
    [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble ] -> [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble LinkDownTagged ]
    [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple ] -> [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple LinkDownTagged ]
    [ LoopIterationCurrent no LinkDown ][ no LinkDown ] -> [ LoopIterationCurrent no LinkDown ][ no LinkDown LinkDownTagged ]

    (etc. with diagonal links)
    [ LoopIterationCurrent LinkUpRightSimple ][ LinkUpRightSimple ] -> [ LoopIterationCurrent LinkUpRightSimple ][ LinkUpRightSimple LinkUpRightTagged ]
    [ LoopIterationCurrent no LinkUpRight ][ no LinkUpRight ] -> [ LoopIterationCurrent no LinkUpRight ][ no LinkUpRight LinkUpRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownRightSimple ][ LinkDownRightSimple ] -> [ LoopIterationCurrent LinkDownRightSimple ][ LinkDownRightSimple LinkDownRightTagged ]
    [ LoopIterationCurrent no LinkDownRight ][ no LinkDownRight ] -> [ LoopIterationCurrent no LinkDownRight ][ no LinkDownRight LinkDownRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownLeftSimple ][ LinkDownLeftSimple ] -> [ LoopIterationCurrent LinkDownLeftSimple ][ LinkDownLeftSimple LinkDownLeftTagged ]
    [ LoopIterationCurrent no LinkDownLeft ][ no LinkDownLeft ] -> [ LoopIterationCurrent no LinkDownLeft ][ no LinkDownLeft LinkDownLeftTagged ]

    (etc.)
    [ LoopIterationCurrent LinkUpLeftSimple ][ LinkUpLeftSimple ] -> [ LoopIterationCurrent LinkUpLeftSimple ][ LinkUpLeftSimple LinkUpLeftTagged ]
    [ LoopIterationCurrent no LinkUpLeft ][ no LinkUpLeft ] -> [ LoopIterationCurrent no LinkUpLeft ][ no LinkUpLeft LinkUpLeftTagged ]

    (If another Atom in the ModelZone has all its characteristics tagged, it means the current Atom is not Unique.)
    (This loop iteration is a fail.)
    [ GlobalTags no GlobalTagFoundUniqueAtom ][ no LoopIterationCurrent ModelZone AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ] -> [ GlobalTags no GlobalTagFoundUniqueAtom GlobalTagSearchUniqueFail ][ ModelZone AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ]

    (If this loop iteration is not a fail, declares the current atom as unique.)
    (The following rule is executed twice :Â once in the ModelZone, once in the ArenaZone.)
    (So that the position of the unique atom is known in both zones.)
    [ GlobalTags no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ][ AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ] -> [ GlobalTags ][ UniqueAtomStartCheck ]

    (Declares that a unique atom has been found.)
    [ GlobalTags no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ][ LoopIterationCurrent ] -> [ GlobalTags GlobalTagFoundUniqueAtom ][ LoopIterationCurrent ]

    (End of loop iteration. Cleans all : tags, loop objects, fail tag.)
    [ AtomTagged ] -> [ ]
    [ LinkTagged ] -> [ ]
    [ LoopIterationCurrent ] -> [ ]
    [ GlobalTagLoopIteration ] -> [ ]
    [ GlobalTagSearchUniqueFail ] -> [ ]

endLoop

(Raise exception if no unique atom is found.)
[ GlobalTags no GlobalTagFoundUniqueAtom ][ Player ] -> [ GlobalTags no GlobalTagFoundUniqueAtom ][ ] message ERROR : no unique element (atom+links). A level must have at least one unique element.

(Cleans all the loop elements that may be left after the search of a unique atom)
[ LoopIterationToDo ] -> [ ]


( --- Handles player and atom/links moves --- )

[ > Player | no ArenaZone ] -> cancel

(Prepares tags to have the "again" rule when player moves an atom.)
(Prepares tags to enable the pattern check when a move is finished.)
LEFT [ GlobalTagSlideLeft ][ Player | no Obstacle ] -> [ GlobalTagSlideLeft ][ LEFT Player | ]
LEFT [ GlobalTags GlobalTagSlideLeft ][ Player | Obstacle ] -> [ GlobalTags GlobalTagMustCheckPattern ][ Player | Obstacle ]
RIGHT [ GlobalTagSlideRight ][ Player | no Obstacle ] -> [ GlobalTagSlideRight ][ RIGHT Player | ]
RIGHT [ GlobalTags GlobalTagSlideRight ][ Player | Obstacle ] -> [ GlobalTags GlobalTagMustCheckPattern ][ Player | Obstacle ]
UP [ GlobalTagSlideUp ][ Player | no Obstacle ] -> [ GlobalTagSlideUp ][ UP Player | ]
UP [ GlobalTags GlobalTagSlideUp ][ Player | Obstacle ] -> [ GlobalTags GlobalTagMustCheckPattern ][ Player | Obstacle ]
DOWN [ GlobalTagSlideDown ][ Player | no Obstacle ] -> [ GlobalTagSlideDown ][ DOWN Player | ]
DOWN [ GlobalTags GlobalTagSlideDown ][ Player | Obstacle ] -> [ GlobalTags GlobalTagMustCheckPattern ][ Player | Obstacle ]

[ GlobalTags GlobalTagMustCheckPattern ][ MovingAtom ] -> [ GlobalTags GlobalTagMustCheckPattern ][ ]

[ > Player SelectedAtom Atom ] -> [ > Player > SelectedAtom > Atom ]
[ > Player SelectedAtom Link ] -> [ > Player > SelectedAtom > Link ]
[ > Player > SelectedAtom ] -> [ > Player > SelectedAtom > MovingAtom ]
[ > Player SelectedAtom UniqueAtomStartCheck ] -> [ > Player SelectedAtom > UniqueAtomStartCheck ]

[ > Atom | Obstacle ] -> cancel
[ > Link | Obstacle ] -> cancel

[ GlobalTags no GlobalTagSlideLeft ][ LEFT SelectedAtom ] -> [ GlobalTags GlobalTagSlideLeft ][ LEFT SelectedAtom ]
[ GlobalTags no GlobalTagSlideRight ][ RIGHT SelectedAtom ] -> [ GlobalTags GlobalTagSlideRight ][ RIGHT SelectedAtom ]
[ GlobalTags no GlobalTagSlideUp ][ UP SelectedAtom ] -> [ GlobalTags GlobalTagSlideUp ][ UP SelectedAtom ]
[ GlobalTags no GlobalTagSlideDown ][ DOWN SelectedAtom ] -> [ GlobalTags GlobalTagSlideDown ][ DOWN SelectedAtom ]
[ GlobalTagSlide ] -> again

[ GlobalTags ][ > UniqueAtomStartCheck ] -> [ GlobalTags GlobalTagRecalculateCheckZone ][ > UniqueAtomStartCheck ]

( --- Grabs or releases selected atom --- )

[ Action Player SelectedAtom ] -> [ Player no SelectedAtom ]
[ Action Player Atom no SelectedAtom ] -> [ Player Atom SelectedAtom ]


( --- Calculates CheckZone --- )
(The checkzone is calculated only if the unique atom moved and if the current sliding is done)
(This is why we always check for both "GlobalTags" "GlobalTagRecalculateCheckZone" during the calculation.)

late [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ CheckZone ] -> [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ ]

late [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ] -> [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck CopyToRight CopyToUp CopyToDown CopyToLeft ]
late [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ArenaZone ] -> [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ArenaZone CheckZone ]

late RIGHT [ ModelZone CopyToRight | ModelZone ][ ArenaZone CopyToRight |  ] -> [ ModelZone | CopyToRight ModelZone ][ ArenaZone | CopyToRight CheckZone ]
late LEFT [ ModelZone CopyToLeft | ModelZone ][ ArenaZone CopyToLeft |  ] -> [ ModelZone | CopyToLeft ModelZone ][ ArenaZone | CopyToLeft CheckZone ]
late UP [ ModelZone CopyToUp | ModelZone ][ ArenaZone CopyToUp |  ] -> [ ModelZone | CopyToUp ModelZone ][ ArenaZone | CopyToUp CheckZone ]
late DOWN [ ModelZone CopyToDown | ModelZone ][ ArenaZone CopyToDown |  ] -> [ ModelZone | CopyToDown ModelZone ][ ArenaZone | CopyToDown CheckZone ]

late [ CopyToUp ] -> [ ]
late [ CopyToRight ] -> [ ]
late [ CopyToDown ] -> [ ]
late [ CopyToLeft ] -> [ ]

(Extend the CheckZone to its bounding rect.)
(No need to fill the gap, because the initial CheckZone is consisted of a cross, which is one block of adjacent tiles.)
late [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ CheckZone ] -> [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ][ CheckZone PropagateVertical PropagateHorizontal ]
late VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
late HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
late [ PropagateHorizontal PropagateVertical ] -> [ CheckZone ]
late [ PropagateVertical ] -> [ ]
late [ PropagateHorizontal ] -> [ ]

late [ GlobalTagMustCheckPattern GlobalTagRecalculateCheckZone ] -> [ GlobalTagMustCheckPattern ]


( --- Checks equality between ModelZone and CheckZone --- )

(Debug only. Comment on release)
[ CurrentCheckedOK ] -> [ ]

(Initiates a loop on every squares of the ModelZone and CheckZone)
(The LoopIterationToDo objects are added two by two, one on the ModelZone and one on the CheckZone.)
(So that there is always the same number of "looped" ModelZone and "looped" CheckZone.)
late [ GlobalTagMustCheckPattern ][ ModelZone no LoopIterationToDo ][ CheckZone no LoopIterationToDo ] -> [ GlobalTagMustCheckPattern ][ ModelZone LoopIterationToDo ][ CheckZone LoopIterationToDo ]
(Checks if there is the same quantity of looped ModelZones and CheckZones. If not, add a GlobalTagCheckZoneUncomplete.)
(In that case, no need to check the pattern, it will inevitably fail.)
(That case may happen if the unique atom is near a border.)
late [ GlobalTags GlobalTagMustCheckPattern ][ ModelZone no LoopIterationToDo ][ CheckZone LoopIterationToDo ] -> [ GlobalTags GlobalTagMustCheckPattern GlobalTagCheckZoneUncomplete ][ ModelZone ][ CheckZone LoopIterationToDo ]
(CheckZone is not complete. We cancel all the pattern-check loop.)
late [ GlobalTagCheckZoneUncomplete ][ LoopIterationToDo ] -> [ GlobalTagCheckZoneUncomplete ][ ]
late [ GlobalTagCheckZoneUncomplete ] -> [ ]

(Starts one loop iteration, on ModelZone and CheckZone at the same time.)
startLoop

late [ GlobalTags no GlobalTagLoopIteration no GlobalTagCheckFailed ][ LoopIterationToDo ModelZone ][ LoopIterationToDo CheckZone ] -> [ GlobalTags GlobalTagLoopIteration ][ LoopIterationCurrent ModelZone ][     LoopIterationCurrent CheckZone ]

    (PuzzleScript, why you no have variables ?)
    late [ LoopIterationCurrent ModelZone no Atom ][ LoopIterationCurrent CheckZone no Atom ] -> [ LoopIterationCurrent ModelZone no Atom CurrentCheckedOK ][ LoopIterationCurrent CheckZone no Atom ]
    late [ LoopIterationCurrent ModelZone Hydrogen ][ LoopIterationCurrent CheckZone Hydrogen ] -> [ LoopIterationCurrent ModelZone Hydrogen CurrentCheckedOK ][ LoopIterationCurrent CheckZone Hydrogen ]
    late [ LoopIterationCurrent ModelZone Carbon ][ LoopIterationCurrent CheckZone Carbon ] -> [ LoopIterationCurrent ModelZone Carbon CurrentCheckedOK ][ LoopIterationCurrent CheckZone Carbon ]
    late [ LoopIterationCurrent ModelZone Oxygen ][ LoopIterationCurrent CheckZone Oxygen ] -> [ LoopIterationCurrent ModelZone Oxygen CurrentCheckedOK ][ LoopIterationCurrent CheckZone Oxygen ]
    late [ LoopIterationCurrent ModelZone Nitrogen ][ LoopIterationCurrent CheckZone Nitrogen ] -> [ LoopIterationCurrent ModelZone Nitrogen CurrentCheckedOK ][ LoopIterationCurrent CheckZone Nitrogen ]
    late [ LoopIterationCurrent ModelZone Sulfur ][ LoopIterationCurrent CheckZone Sulfur ] -> [ LoopIterationCurrent ModelZone Sulfur CurrentCheckedOK ][ LoopIterationCurrent CheckZone Sulfur ]
    late [ LoopIterationCurrent ModelZone Chlorine ][ LoopIterationCurrent CheckZone Chlorine ] -> [ LoopIterationCurrent ModelZone Chlorine CurrentCheckedOK ][ LoopIterationCurrent CheckZone Chlorine ]
    late [ LoopIterationCurrent ModelZone Phosphore ][ LoopIterationCurrent CheckZone Phosphore ] -> [ LoopIterationCurrent ModelZone Phosphore CurrentCheckedOK ][ LoopIterationCurrent CheckZone Phosphore ]
    late [ LoopIterationCurrent ModelZone Cristal ][ LoopIterationCurrent CheckZone Cristal ] -> [ LoopIterationCurrent ModelZone Cristal CurrentCheckedOK ][ LoopIterationCurrent CheckZone Cristal ]
    late [ LoopIterationCurrent ModelZone EmptyAtom ][ LoopIterationCurrent CheckZone EmptyAtom ] -> [ LoopIterationCurrent ModelZone EmptyAtom CurrentCheckedOK ][ LoopIterationCurrent CheckZone EmptyAtom ]
    late [ LoopIterationCurrent ModelZone Fluorine ][ LoopIterationCurrent CheckZone Fluorine ] -> [ LoopIterationCurrent ModelZone Fluorine CurrentCheckedOK ][ LoopIterationCurrent CheckZone Fluorine ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (checks 4-dirs links.)

    late [ LoopIterationCurrent ModelZone no LinkLeft ][ LoopIterationCurrent CheckZone no LinkLeft ] -> [ LoopIterationCurrent ModelZone no LinkLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkLeft ]
    late [ LoopIterationCurrent ModelZone LinkLeftSimple ][ LoopIterationCurrent CheckZone LinkLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftSimple ]
    late [ LoopIterationCurrent ModelZone LinkLeftDouble ][ LoopIterationCurrent CheckZone LinkLeftDouble ] -> [ LoopIterationCurrent ModelZone LinkLeftDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftDouble ]
    late [ LoopIterationCurrent ModelZone LinkLeftTriple ][ LoopIterationCurrent CheckZone LinkLeftTriple ] -> [ LoopIterationCurrent ModelZone LinkLeftTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftTriple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkRight ][ LoopIterationCurrent CheckZone no LinkRight ] -> [ LoopIterationCurrent ModelZone no LinkRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkRight ]
    late [ LoopIterationCurrent ModelZone LinkRightSimple ][ LoopIterationCurrent CheckZone LinkRightSimple ] -> [ LoopIterationCurrent ModelZone LinkRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightSimple ]
    late [ LoopIterationCurrent ModelZone LinkRightDouble ][ LoopIterationCurrent CheckZone LinkRightDouble ] -> [ LoopIterationCurrent ModelZone LinkRightDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightDouble ]
    late [ LoopIterationCurrent ModelZone LinkRightTriple ][ LoopIterationCurrent CheckZone LinkRightTriple ] -> [ LoopIterationCurrent ModelZone LinkRightTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightTriple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkUp ][ LoopIterationCurrent CheckZone no LinkUp ] -> [ LoopIterationCurrent ModelZone no LinkUp CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUp ]
    late [ LoopIterationCurrent ModelZone LinkUpSimple ][ LoopIterationCurrent CheckZone LinkUpSimple ] -> [ LoopIterationCurrent ModelZone LinkUpSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpSimple ]
    late [ LoopIterationCurrent ModelZone LinkUpDouble ][ LoopIterationCurrent CheckZone LinkUpDouble ] -> [ LoopIterationCurrent ModelZone LinkUpDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpDouble ]
    late [ LoopIterationCurrent ModelZone LinkUpTriple ][ LoopIterationCurrent CheckZone LinkUpTriple ] -> [ LoopIterationCurrent ModelZone LinkUpTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpTriple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDown ][ LoopIterationCurrent CheckZone no LinkDown ] -> [ LoopIterationCurrent ModelZone no LinkDown CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDown ]
    late [ LoopIterationCurrent ModelZone LinkDownSimple ][ LoopIterationCurrent CheckZone LinkDownSimple ] -> [ LoopIterationCurrent ModelZone LinkDownSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownSimple ]
    late [ LoopIterationCurrent ModelZone LinkDownDouble ][ LoopIterationCurrent CheckZone LinkDownDouble ] -> [ LoopIterationCurrent ModelZone LinkDownDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownDouble ]
    late [ LoopIterationCurrent ModelZone LinkDownTriple ][ LoopIterationCurrent CheckZone LinkDownTriple ] -> [ LoopIterationCurrent ModelZone LinkDownTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownTriple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (checks diagonal links.)

    late [ LoopIterationCurrent ModelZone no LinkUpRight ][ LoopIterationCurrent CheckZone no LinkUpRight ] -> [ LoopIterationCurrent ModelZone no LinkUpRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUpRight ]
    late [ LoopIterationCurrent ModelZone LinkUpRightSimple ][ LoopIterationCurrent CheckZone LinkUpRightSimple ] -> [ LoopIterationCurrent ModelZone LinkUpRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpRightSimple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDownRight ][ LoopIterationCurrent CheckZone no LinkDownRight ] -> [ LoopIterationCurrent ModelZone no LinkDownRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDownRight ]
    late [ LoopIterationCurrent ModelZone LinkDownRightSimple ][ LoopIterationCurrent CheckZone LinkDownRightSimple ] -> [ LoopIterationCurrent ModelZone LinkDownRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownRightSimple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDownLeft ][ LoopIterationCurrent CheckZone no LinkDownLeft ] -> [ LoopIterationCurrent ModelZone no LinkDownLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDownLeft ]
    late [ LoopIterationCurrent ModelZone LinkDownLeftSimple ][ LoopIterationCurrent CheckZone LinkDownLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkDownLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownLeftSimple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkUpLeft ][ LoopIterationCurrent CheckZone no LinkUpLeft ] -> [ LoopIterationCurrent ModelZone no LinkUpLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUpLeft ]
    late [ LoopIterationCurrent ModelZone LinkUpLeftSimple ][ LoopIterationCurrent CheckZone LinkUpLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkUpLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpLeftSimple ]

    late [ GlobalTags ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTags GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (Debug only. Comment on release)
    late [ GlobalTags no GlobalTagCheckFailed ][ LoopIterationCurrent ] -> [ GlobalTags ][ LoopIterationCurrent CurrentCheckedOK ]

    late [ LoopIterationCurrent ] -> [ ]
    late [ GlobalTagLoopIteration ] -> [ ]

endLoop

late random [ GlobalTags GlobalTagMustCheckPattern no GlobalTagCheckFailed ][ ArenaZone Atom ] -> [ GlobalTags GlobalTagMustCheckPattern GlobalTagWon ][ ArenaZone WinStar1 ]
late [ GlobalTagWon ][ Player SelectedAtom ] -> [ GlobalTagWon ][ ]

late [ GlobalTagCheckFailed] -> [ ]
late [ LoopIterationToDo] -> [ ]
late [ GlobalTagMustCheckPattern ] -> [ ]

(The evolution of WinStars must be written in reversed order, to prevent a WinStar0 to be replaced by a WinStar3 in one turn)
(Anyway, because it loops, the 3 following actions are made in one turn :)
(removing of WinStar3, creation of  WinStar0, replacement of WinStar0 by WinStar1)
(This is why WinStar0 is a transparent object)
late random [ WinStar4 ][ ArenaZone Atom ] -> [ ][ ArenaZone WinStar0 ]
late [ WinStar4 ] -> [ ]
late [ WinStar3 ] -> [ WinStar4 ]
late [ WinStar2 ] -> [ WinStar3 ]
late [ WinStar1 ] -> [ WinStar2 ]
late [ WinStar0 ] -> [ WinStar1 ]
late [ GlobalTagWon ] -> again

late [ WinStar Link ] -> [ WinStar ]

==============
WINCONDITIONS
==============

Some GlobalTagWon
no Atom on ArenaZone
no WinStar


=======
LEVELS
=======

(Test to validate the detection of zones overlapping)
(#######)
(#....A#)
(#.#####)
(#.#.AB#)
(#B#...#)
(###*..#)
(###...#)
(###...#)
(#######)

(Test to validate the detection of lack of model zone)
(#######)
(#....A#)
(#.#####)
(#.#.AB#)
(#B....#)
(###*..#)
(###...#)
(###...#)
(#######)

(Test to validate the detection of lack of global tag)
(..A#..)
(..##.*)
(..#...)
(B.##AB)

(Test to validate the detection of lack of unique element)
(#######)
(#..#.*#)
(#AB#AA#)
(#AB#BB#)
(#######)

(Test to validate all the atom-related rules, and the atoms appearance)
(Must have the following values in the legend)
(0 = Hydrogen)
(1 = Carbon)
(2 = Oxygen)
(3 = Nitrogen)
(4 = Sulfur)
(5 = Chlorine)
(6 = Phosphore)
(7 = Cristal)
(8 = EmptyAtom and LinkUpSimple and LinkDownSimple)
(9 = Fluorine)
()
(############)
(#0123456789#)
(############)
(#....*.....#)
(#5742690138#)
(############)

.....#####..
.....#...##.
.....#...F#.
.....#.*..#.
.BF..#..B.##
...E.#...#E#
.....#######


#######
#.BF..#
#...E.#
#######
#..B###
#....##
#.*..##
#...F##
#...#E#
#######


#############
#.BF..#######
#...E.#######
#############
#B..#########
#....#####..#
#.*..####.###
#....#####.##
#F..#E....###
#############


###############
#..H..........#
#.BCDABA......#
#..H..........#
#..H..........#
###############
#....D........#
###.....H.A...#
#.#....BC.....#
###.....H*....#
#.AAA.......H.#
###############



##################
#..H..........####
#.HAABCA......####
#..H..........####
##################
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################



##################
#.HHAC........####
#.HAAB........####
#.............####
##################
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################
