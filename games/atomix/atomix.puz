title Atomix demake
author Recher
homepage recher.wordpress.com

(condition : il faut au moins un mur)
(TODO : expliquer comment faire le modèle)
(TODO : expliquer qu'il faut une marge sur les bords, pour que le rectangle de la CheckZone puisse avoir la place de se déplacer)

verbose_logging
again_interval 0.6

run_rules_on_level_start

========
OBJECTS
========

Background
black

Wall
lightgray

ModelZone
darkgray

ArenaZone
#686018

Hydrogen
transparent gray
00000
01110
01110
01110
00000

Carbon
transparent blue
00000
01110
01110
01110
00000

Oxygen
transparent red
00000
01110
01110
01110
00000

AtomTagged
transparent

AtomCheckedOK
transparent

SelectedAtom
transparent yellow
10101
00000
10001
00000
10101

Player
transparent orange
10101
00000
10001
00000
10101

UniqueAtomStartCheck
(debug = red point. release = transparent)
(transparent)
transparent red
00000
00000
00100
00000
00000

CopyToRight
transparent

CopyToUp
transparent

CopyToDown
transparent

CopyToLeft
transparent

CheckZone
(debug = purple. release = transparent)
(transparent)
purple

LinkLeftSimple
transparent #eeeeee
00000
00000
11100
00000
00000

LinkLeftDouble
transparent #eeeeee
00000
11100
00000
11100
00000

LinkLeftTriple
transparent #eeeeee
00000
11100
11100
11100
00000

LinkLeftTagged
transparent

LinkLeftCheckedOK
transparent

LinkRightSimple
transparent #eeeeee
00000
00000
00111
00000
00000

LinkRightDouble
transparent #eeeeee
00000
00111
00000
00111
00000

LinkRightTriple
transparent #eeeeee
00000
00111
00111
00111
00000

LinkRightTagged
transparent

LinkRightCheckedOK
transparent

LinkUpSimple
transparent #eeeeee
00100
00100
00100
00000
00000

LinkUpDouble
transparent #eeeeee
01010
01010
01010
00000
00000

LinkUpTriple
transparent #eeeeee
01110
01110
01110
00000
00000

LinkUpTagged
transparent

LinkUpCheckedOK
transparent

LinkDownSimple
transparent #eeeeee
00000
00000
00100
00100
00100

LinkDownDouble
transparent #eeeeee
00000
00000
01010
01010
01010

LinkDownTriple
transparent #eeeeee
00000
00000
01110
01110
01110

LinkDownTagged
transparent

LinkDownCheckedOK
transparent

LinkUpRightSimple
transparent #eeeeee
00001
00010
00100
00000
00000

LinkUpRightTagged
transparent

LinkUpRightCheckedOK
transparent

LinkDownRightSimple
transparent #eeeeee
00000
00000
00100
00010
00001

LinkDownRightTagged
transparent

LinkDownRightCheckedOK
transparent

LinkDownLeftSimple
transparent #eeeeee
00000
00000
00100
01000
10000

LinkDownLeftTagged
transparent

LinkDownLeftCheckedOK
transparent

LinkUpLeftSimple
transparent #eeeeee
10000
01000
00100
00000
00000

LinkUpLeftTagged
transparent

LinkUpLeftCheckedOK
transparent


GlobalTagPosition
(debug = #FF00FF. release = transparent)
#FF00FF
(transparent)
(TODO : rename to GlobalTag)

GlobalTagFoundUniqueAtom
transparent

GlobalTagSearchUniqueFail
transparent

GlobalTagLoopIteration
transparent

GlobalTagRecalculateCheckZone
transparent

GlobalTagCheckZoneComplete
transparent

GlobalTagCheckFailed
transparent

LoopIterationToDo
transparent

LoopIterationCurrent
transparent

(TODO : replace with PlayerTagMustDoInit and reverse everything, so that the Player will not have to move with its PlayerTagInitDone)
PlayerTagInitDone
transparent

PropagateVertical
transparent

PropagateHorizontal
transparent

CurrentCheckedOK
(debug = pink. release = transparent)
pink
(transparent)

PropagateDiagonalHalfWay
transparent

GlobalTagSlideLeft
transparent

GlobalTagSlideRight
transparent

GlobalTagSlideUp
transparent

GlobalTagSlideDown
transparent

GlobalTagMustCheckPattern
transparent

=======
LEGEND
=======

. = Background
# = Wall
* = Player

A = Hydrogen and LinkLeftSimple
B = Hydrogen and LinkRightSimple
C = Hydrogen and LinkUpSimple
D = Hydrogen and LinkDownSimple
E = Hydrogen and LinkUpLeftSimple
F = Oxygen and LinkLeftSimple and LinkDownRightSimple
H = Hydrogen
O = Oxygen
Z = Carbon and LinkLeftSimple and LinkRightSimple

Atom = Hydrogen or Carbon or Oxygen
LinkLeft = LinkLeftSimple or LinkLeftDouble or LinkLeftTriple
LinkRight = LinkRightSimple or LinkRightDouble or LinkRightTriple
LinkUp = LinkUpSimple or LinkUpDouble or LinkUpTriple
LinkDown = LinkDownSimple or LinkDownDouble or LinkDownTriple
LinkUpRight = LinkUpRightSimple
LinkDownRight = LinkDownRightSimple
LinkDownLeft = LinkDownLeftSimple
LinkUpLeft = LinkUpLeftSimple
Link = LinkLeft or LinkRight or LinkUp or LinkDown or LinkUpRight or LinkDownRight or LinkDownLeft or LinkUpLeft

Obstacle = Wall or Atom or Link

GlobalTagSlide = GlobalTagSlideLeft or GlobalTagSlideRight or GlobalTagSlideUp or GlobalTagSlideDown

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background

ModelZone
ArenaZone

CheckZone

CurrentCheckedOK

LinkLeftSimple, LinkLeftDouble, LinkLeftTriple
LinkLeftTagged
LinkLeftCheckedOK
LinkRightSimple, LinkRightDouble, LinkRightTriple
LinkRightTagged
LinkRightCheckedOK
LinkUpSimple, LinkUpDouble, LinkUpTriple
LinkUpTagged
LinkUpCheckedOK
LinkDownSimple, LinkDownDouble, LinkDownTriple
LinkDownTagged
LinkDownCheckedOK

LinkUpRightSimple
LinkUpRightTagged
LinkUpRightCheckedOK
LinkDownRightSimple
LinkDownRightTagged
LinkDownRightCheckedOK
LinkDownLeftSimple
LinkDownLeftTagged
LinkDownLeftCheckedOK
LinkUpLeftSimple
LinkUpLeftTagged
LinkUpLeftCheckedOK

Wall, Hydrogen, Carbon, Oxygen
AtomTagged
AtomCheckedOK

Player
SelectedAtom

UniqueAtomStartCheck

CopyToRight
CopyToUp
CopyToDown
CopyToLeft

GlobalTagPosition
GlobalTagFoundUniqueAtom
GlobalTagSearchUniqueFail
GlobalTagLoopIteration
GlobalTagRecalculateCheckZone
GlobalTagCheckZoneComplete
GlobalTagCheckFailed
GlobalTagSlideLeft, GlobalTagSlideRight, GlobalTagSlideUp, GlobalTagSlideDown
GlobalTagMustCheckPattern

LoopIterationToDo
LoopIterationCurrent

PlayerTagInitDone

PropagateVertical
PropagateHorizontal
PropagateDiagonalHalfWay

======
RULES
======


( --- Detects the ArenaZone ---)

( Global tags does not exist at the beginning of the game,)
(so we use a global tag on the player.)

[ Player no PlayerTagInitDone ] -> [ Player ArenaZone ]

(propagates ArenaZone, starting from player)
(Propagation can be made diagonally. Like in the first level of Draknek)
startLoop
    [ Player no PlayerTagInitDone ][ ArenaZone | no Wall ] -> [ Player no PlayerTagInitDone ][ ArenaZone | ArenaZone ]
    VERTICAL [ Player no PlayerTagInitDone ][ ArenaZone | ] -> [ Player no PlayerTagInitDone ][ ArenaZone | PropagateDiagonalHalfWay ]
    HORIZONTAL [ Player no PlayerTagInitDone ][ PropagateDiagonalHalfWay | no Wall ] -> [ Player no PlayerTagInitDone ][ PropagateDiagonalHalfWay | ArenaZone ]
endLoop

[ Player no PlayerTagInitDone ][ PropagateDiagonalHalfWay ] -> [ Player no PlayerTagInitDone ][ ]


[ Player no PlayerTagInitDone ][ ArenaZone ] -> [ Player no PlayerTagInitDone ][ ArenaZone PropagateVertical PropagateHorizontal ]

(TODO : explain this)
VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
[ PropagateHorizontal PropagateVertical ] -> [ ArenaZone ]
[ PropagateVertical ] -> [ ]
[ PropagateHorizontal ] -> [ ]


( --- Detects the ModelZone ---)

[ Player no PlayerTagInitDone ][ Atom no ArenaZone ] -> [ Player no PlayerTagInitDone ][ Atom ModelZone ]
[ Player no PlayerTagInitDone ][ Link no ArenaZone ] -> [ Player no PlayerTagInitDone ][ Link ModelZone ]
[ Player no PlayerTagInitDone ][ ModelZone ] -> [ Player no PlayerTagInitDone ][ ModelZone PropagateVertical PropagateHorizontal ]

(TODO : explain this)
VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
[ PropagateHorizontal PropagateVertical ] -> [ ModelZone ]
[ PropagateVertical ] -> [ ]
[ PropagateHorizontal ] -> [ ]


(--- Puts the GlobalTag anywhere in a "nothing-zone" --- )

(Adds also the global tag to recalculate the CheckZone a first time.)

[ Player no PlayerTagInitDone ][no ArenaZone no ModelZone ] -> [ Player PlayerTagInitDone ][ GlobalTagPosition GlobalTagRecalculateCheckZone ]

(TODO : raise exception if ModelZone and ArenaZone overlaps)


( --- Searches for a unique atom in the ModelZone --- )

(Initiate a loop on all the Atoms and Link of the ModelZone)
[ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ ModelZone Atom ] -> [ GlobalTagPosition ][ ModelZone Atom LoopIterationToDo ]
[ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ ModelZone Link ] -> [ GlobalTagPosition ][ ModelZone Link LoopIterationToDo ]

(Starts one loop iteration, while no unique atom is found.)
startLoop

    [ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagLoopIteration ][ LoopIterationToDo ] -> [ GlobalTagPosition GlobalTagLoopIteration ][ LoopIterationCurrent ]

    (Tags all the atoms that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no Atom' case))
    [ LoopIterationCurrent Hydrogen ][ Hydrogen ] -> [ LoopIterationCurrent Hydrogen ][ Hydrogen AtomTagged ]
    [ LoopIterationCurrent Carbon ][ Carbon ] -> [ LoopIterationCurrent Carbon ][ Carbon AtomTagged ]
    [ LoopIterationCurrent Oxygen ][ Oxygen ] -> [ LoopIterationCurrent Oxygen ][ Oxygen AtomTagged ]
    [ LoopIterationCurrent no Atom ][ no Atom ] -> [ LoopIterationCurrent no Atom ][ no Atom AtomTagged ]

    (Tags all the LinkLeft that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no LinkLeft' case))
    [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple ] -> [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple LinkLeftTagged ]
    [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble ] -> [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble LinkLeftTagged ]
    [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple ] -> [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple LinkLeftTagged ]
    [ LoopIterationCurrent no LinkLeft ][ no LinkLeft ] -> [ LoopIterationCurrent no LinkLeft ][ no LinkLeft LinkLeftTagged ]

    (Tags all the LinkRight, etc.)
    [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple ] -> [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple LinkRightTagged ]
    [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble ] -> [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble LinkRightTagged ]
    [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple ] -> [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple LinkRightTagged ]
    [ LoopIterationCurrent no LinkRight ][ no LinkRight ] -> [ LoopIterationCurrent no LinkRight ][ no LinkRight LinkRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple ] -> [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple LinkUpTagged ]
    [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble ] -> [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble LinkUpTagged ]
    [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple ] -> [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple LinkUpTagged ]
    [ LoopIterationCurrent no LinkUp ][ no LinkUp ] -> [ LoopIterationCurrent no LinkUp ][ no LinkUp LinkUpTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple ] -> [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple LinkDownTagged ]
    [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble ] -> [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble LinkDownTagged ]
    [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple ] -> [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple LinkDownTagged ]
    [ LoopIterationCurrent no LinkDown ][ no LinkDown ] -> [ LoopIterationCurrent no LinkDown ][ no LinkDown LinkDownTagged ]

    (etc. with diagonal links)
    [ LoopIterationCurrent LinkUpRightSimple ][ LinkUpRightSimple ] -> [ LoopIterationCurrent LinkUpRightSimple ][ LinkUpRightSimple LinkUpRightTagged ]
    [ LoopIterationCurrent no LinkUpRight ][ no LinkUpRight ] -> [ LoopIterationCurrent no LinkUpRight ][ no LinkUpRight LinkUpRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownRightSimple ][ LinkDownRightSimple ] -> [ LoopIterationCurrent LinkDownRightSimple ][ LinkDownRightSimple LinkDownRightTagged ]
    [ LoopIterationCurrent no LinkDownRight ][ no LinkDownRight ] -> [ LoopIterationCurrent no LinkDownRight ][ no LinkDownRight LinkDownRightTagged ]

    (etc.)
    [ LoopIterationCurrent LinkDownLeftSimple ][ LinkDownLeftSimple ] -> [ LoopIterationCurrent LinkDownLeftSimple ][ LinkDownLeftSimple LinkDownLeftTagged ]
    [ LoopIterationCurrent no LinkDownLeft ][ no LinkDownLeft ] -> [ LoopIterationCurrent no LinkDownLeft ][ no LinkDownLeft LinkDownLeftTagged ]

    (etc.)
    [ LoopIterationCurrent LinkUpLeftSimple ][ LinkUpLeftSimple ] -> [ LoopIterationCurrent LinkUpLeftSimple ][ LinkUpLeftSimple LinkUpLeftTagged ]
    [ LoopIterationCurrent no LinkUpLeft ][ no LinkUpLeft ] -> [ LoopIterationCurrent no LinkUpLeft ][ no LinkUpLeft LinkUpLeftTagged ]

    (If another Atom in the ModelZone has all its characteristics tagged, it means the current Atom is not Unique.)
    (This loop iteration is a fail.)
    [ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ no LoopIterationCurrent ModelZone AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ] -> [     GlobalTagPosition no GlobalTagFoundUniqueAtom GlobalTagSearchUniqueFail ][ ModelZone AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ]

    (If this loop iteration is not a fail, declares the current atom as unique.)
    (The following rule is executed twice : once in the ModelZone, once in the ArenaZone.)
    (So that the position of the unique atom is known in both zones.)
    [ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ][ AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged LinkUpRightTagged LinkDownRightTagged LinkDownLeftTagged LinkUpLeftTagged ] -> [     GlobalTagPosition ][ UniqueAtomStartCheck ]

    (Declares that a unique atom has been found.)
    [ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ] -> [ GlobalTagPosition GlobalTagFoundUniqueAtom ]

    (End of loop iteration. Cleans all : tags, loop objects, fail tag.)
    [ AtomTagged ] -> [ ]
    [ LinkLeftTagged ] -> [ ]
    [ LinkRightTagged ] -> [ ]
    [ LinkUpTagged ] -> [ ]
    [ LinkDownTagged ] -> [ ]
    [ LoopIterationCurrent ] -> [ ]
    [ GlobalTagLoopIteration ] -> [ ]
    [ GlobalTagSearchUniqueFail ] -> [ ]

endLoop

( --- Handles player and atom/links moves --- )

[ > Player | no ArenaZone ] -> cancel

LEFT [ GlobalTagSlideLeft ][ Player | no Obstacle ] -> [ GlobalTagSlideLeft ][ LEFT Player | ]
LEFT [ GlobalTagPosition GlobalTagSlideLeft ][ Player | Obstacle ] -> [ GlobalTagPosition GlobalTagMustCheckPattern ][ Player | Obstacle ] (message must check)
RIGHT [ GlobalTagSlideRight ][ Player | no Obstacle ] -> [ GlobalTagSlideRight ][ RIGHT Player | ]
RIGHT [ GlobalTagPosition GlobalTagSlideRight ][ Player | Obstacle ] -> [ GlobalTagPosition GlobalTagMustCheckPattern ][ Player | Obstacle ] (message must check)
UP [ GlobalTagSlideUp ][ Player | no Obstacle ] -> [ GlobalTagSlideUp ][ UP Player | ]
UP [ GlobalTagPosition GlobalTagSlideUp ][ Player | Obstacle ] -> [ GlobalTagPosition GlobalTagMustCheckPattern ][ Player | Obstacle ] (message must check)
DOWN [ GlobalTagSlideDown ][ Player | no Obstacle ] -> [ GlobalTagSlideDown ][ DOWN Player | ]
DOWN [ GlobalTagPosition GlobalTagSlideDown ][ Player | Obstacle ] -> [ GlobalTagPosition GlobalTagMustCheckPattern ][ Player | Obstacle ] (message must check)

(TODO : "again" rule when player moves an atom. do not check model equality while moving)
[ > Player PlayerTagInitDone ] -> [ > Player > PlayerTagInitDone ]

[ > Player SelectedAtom Atom ] -> [ > Player > SelectedAtom > Atom ]
[ > Player SelectedAtom Link ] -> [ > Player > SelectedAtom > Link ]
[ > Player SelectedAtom UniqueAtomStartCheck ] -> [ > Player SelectedAtom > UniqueAtomStartCheck ]

[ > Atom | Obstacle ] -> cancel
[ > Link | Obstacle ] -> cancel

[ GlobalTagPosition no GlobalTagSlideLeft ][ LEFT SelectedAtom ] -> [ GlobalTagPosition GlobalTagSlideLeft ][ LEFT SelectedAtom ]
[ GlobalTagPosition no GlobalTagSlideRight ][ RIGHT SelectedAtom ] -> [ GlobalTagPosition GlobalTagSlideRight ][ RIGHT SelectedAtom ]
[ GlobalTagPosition no GlobalTagSlideUp ][ UP SelectedAtom ] -> [ GlobalTagPosition GlobalTagSlideUp ][ UP SelectedAtom ]
[ GlobalTagPosition no GlobalTagSlideDown ][ DOWN SelectedAtom ] -> [ GlobalTagPosition GlobalTagSlideDown ][ DOWN SelectedAtom ]
[ GlobalTagSlide ] -> again

[ GlobalTagPosition ][ > UniqueAtomStartCheck ] -> [ GlobalTagPosition GlobalTagRecalculateCheckZone ][ > UniqueAtomStartCheck ]

( --- Grabs or releases selected atom --- )

[ Action Player SelectedAtom ] -> [ Player no SelectedAtom ]
[ Action Player Atom no SelectedAtom ] -> [ Player Atom SelectedAtom ]


( --- Initiates or recalculates CheckZone --- )

(No need to recalculate the check zone if we do not have to check the patterns. This case occurs when an atom is moving.)
(TODO : does not work. Will have to do it in another way.)
([ GlobalTagPosition GlobalTagRecalculateCheckZone no GlobalTagMustCheckPattern ] -> [ GlobalTagPosition ])

late [ GlobalTagRecalculateCheckZone ][ CheckZone ] -> [ GlobalTagRecalculateCheckZone ][ ]

late [ GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ] -> [ GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck CopyToRight CopyToUp CopyToDown CopyToLeft ]
late [ GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ArenaZone ] -> [ GlobalTagRecalculateCheckZone ][ UniqueAtomStartCheck ArenaZone CheckZone ]

late RIGHT [ GlobalTagRecalculateCheckZone ][ ModelZone CopyToRight | ModelZone ][ ArenaZone CopyToRight |  ] -> [ GlobalTagRecalculateCheckZone ][ ModelZone | CopyToRight ModelZone ][ ArenaZone | CopyToRight CheckZone ]
late LEFT [ GlobalTagRecalculateCheckZone ][ ModelZone CopyToLeft | ModelZone ][ ArenaZone CopyToLeft |  ] -> [ GlobalTagRecalculateCheckZone ][ ModelZone | CopyToLeft ModelZone ][ ArenaZone | CopyToLeft CheckZone ]
late UP [ GlobalTagRecalculateCheckZone ][ ModelZone CopyToUp | ModelZone ][ ArenaZone CopyToUp |  ] -> [ GlobalTagRecalculateCheckZone ][ ModelZone | CopyToUp ModelZone ][ ArenaZone | CopyToUp CheckZone ]
late DOWN [ GlobalTagRecalculateCheckZone ][ ModelZone CopyToDown | ModelZone ][ ArenaZone CopyToDown |  ] -> [ GlobalTagRecalculateCheckZone ][ ModelZone | CopyToDown ModelZone ][ ArenaZone | CopyToDown CheckZone ]

late [ CopyToUp ] -> [ ]
late [ CopyToRight ] -> [ ]
late [ CopyToDown ] -> [ ]
late [ CopyToLeft ] -> [ ]

late [ GlobalTagRecalculateCheckZone ][ CheckZone ] -> [ GlobalTagRecalculateCheckZone ][ CheckZone PropagateVertical PropagateHorizontal ]

(TODO : explain this. Group with line above. Same with other procedures.)
late VERTICAL [ PropagateVertical | ] -> [ PropagateVertical | PropagateVertical ]
late HORIZONTAL [ PropagateHorizontal | ] -> [ PropagateHorizontal | PropagateHorizontal ]
late [ PropagateHorizontal PropagateVertical ] -> [ CheckZone ]
late [ PropagateVertical ] -> [ ]
late [ PropagateHorizontal ] -> [ ]

(TODO : counts the CheckZone elements. If same quantity as ModelZone, the equality checks can be made)
late [ GlobalTagPosition ]  -> [ GlobalTagPosition GlobalTagCheckZoneComplete ]

late [ GlobalTagRecalculateCheckZone ] -> [ ]


( --- Checks equality between ModelZone and CheckZone --- )

(Debug only. Comment on release)
[ CurrentCheckedOK ] -> [ ]

(Loops on every square of the ModelZone and CheckZone)
late [ GlobalTagMustCheckPattern GlobalTagCheckZoneComplete ][ ModelZone ] -> [ GlobalTagMustCheckPattern GlobalTagCheckZoneComplete ][ ModelZone LoopIterationToDo ]
late [ GlobalTagMustCheckPattern GlobalTagCheckZoneComplete ][ CheckZone ] -> [ GlobalTagMustCheckPattern GlobalTagCheckZoneComplete ][ CheckZone LoopIterationToDo ]

(Starts one loop iteration, on ModelZone and CheckZone at the same time.)
startLoop

late [ GlobalTagPosition no GlobalTagLoopIteration no GlobalTagCheckFailed ][ LoopIterationToDo ModelZone ][ LoopIterationToDo CheckZone ] -> [ GlobalTagPosition GlobalTagLoopIteration ][ LoopIterationCurrent ModelZone ][     LoopIterationCurrent CheckZone ]

    (PuzzleScript, why you no have variables ?)
    late [ LoopIterationCurrent ModelZone no Atom ][ LoopIterationCurrent CheckZone no Atom ] -> [ LoopIterationCurrent ModelZone no Atom CurrentCheckedOK ][ LoopIterationCurrent CheckZone no Atom ]
    late [ LoopIterationCurrent ModelZone Hydrogen ][ LoopIterationCurrent CheckZone Hydrogen ] -> [ LoopIterationCurrent ModelZone Hydrogen CurrentCheckedOK ][ LoopIterationCurrent CheckZone Hydrogen ]
    late [ LoopIterationCurrent ModelZone Carbon ][ LoopIterationCurrent CheckZone Carbon ] -> [ LoopIterationCurrent ModelZone Carbon CurrentCheckedOK ][ LoopIterationCurrent CheckZone Carbon ]
    late [ LoopIterationCurrent ModelZone Oxygen ][ LoopIterationCurrent CheckZone Oxygen ] -> [ LoopIterationCurrent ModelZone Oxygen CurrentCheckedOK ][ LoopIterationCurrent CheckZone Oxygen ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (checks 4-dirs links.)

    late [ LoopIterationCurrent ModelZone no LinkLeft ][ LoopIterationCurrent CheckZone no LinkLeft ] -> [ LoopIterationCurrent ModelZone no LinkLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkLeft ]
    late [ LoopIterationCurrent ModelZone LinkLeftSimple ][ LoopIterationCurrent CheckZone LinkLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftSimple ]
    late [ LoopIterationCurrent ModelZone LinkLeftDouble ][ LoopIterationCurrent CheckZone LinkLeftDouble ] -> [ LoopIterationCurrent ModelZone LinkLeftDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftDouble ]
    late [ LoopIterationCurrent ModelZone LinkLeftTriple ][ LoopIterationCurrent CheckZone LinkLeftTriple ] -> [ LoopIterationCurrent ModelZone LinkLeftTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkLeftTriple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkRight ][ LoopIterationCurrent CheckZone no LinkRight ] -> [ LoopIterationCurrent ModelZone no LinkRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkRight ]
    late [ LoopIterationCurrent ModelZone LinkRightSimple ][ LoopIterationCurrent CheckZone LinkRightSimple ] -> [ LoopIterationCurrent ModelZone LinkRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightSimple ]
    late [ LoopIterationCurrent ModelZone LinkRightDouble ][ LoopIterationCurrent CheckZone LinkRightDouble ] -> [ LoopIterationCurrent ModelZone LinkRightDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightDouble ]
    late [ LoopIterationCurrent ModelZone LinkRightTriple ][ LoopIterationCurrent CheckZone LinkRightTriple ] -> [ LoopIterationCurrent ModelZone LinkRightTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkRightTriple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkUp ][ LoopIterationCurrent CheckZone no LinkUp ] -> [ LoopIterationCurrent ModelZone no LinkUp CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUp ]
    late [ LoopIterationCurrent ModelZone LinkUpSimple ][ LoopIterationCurrent CheckZone LinkUpSimple ] -> [ LoopIterationCurrent ModelZone LinkUpSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpSimple ]
    late [ LoopIterationCurrent ModelZone LinkUpDouble ][ LoopIterationCurrent CheckZone LinkUpDouble ] -> [ LoopIterationCurrent ModelZone LinkUpDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpDouble ]
    late [ LoopIterationCurrent ModelZone LinkUpTriple ][ LoopIterationCurrent CheckZone LinkUpTriple ] -> [ LoopIterationCurrent ModelZone LinkUpTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpTriple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDown ][ LoopIterationCurrent CheckZone no LinkDown ] -> [ LoopIterationCurrent ModelZone no LinkDown CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDown ]
    late [ LoopIterationCurrent ModelZone LinkDownSimple ][ LoopIterationCurrent CheckZone LinkDownSimple ] -> [ LoopIterationCurrent ModelZone LinkDownSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownSimple ]
    late [ LoopIterationCurrent ModelZone LinkDownDouble ][ LoopIterationCurrent CheckZone LinkDownDouble ] -> [ LoopIterationCurrent ModelZone LinkDownDouble CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownDouble ]
    late [ LoopIterationCurrent ModelZone LinkDownTriple ][ LoopIterationCurrent CheckZone LinkDownTriple ] -> [ LoopIterationCurrent ModelZone LinkDownTriple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownTriple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (checks diagonal links.)

    late [ LoopIterationCurrent ModelZone no LinkUpRight ][ LoopIterationCurrent CheckZone no LinkUpRight ] -> [ LoopIterationCurrent ModelZone no LinkUpRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUpRight ]
    late [ LoopIterationCurrent ModelZone LinkUpRightSimple ][ LoopIterationCurrent CheckZone LinkUpRightSimple ] -> [ LoopIterationCurrent ModelZone LinkUpRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpRightSimple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDownRight ][ LoopIterationCurrent CheckZone no LinkDownRight ] -> [ LoopIterationCurrent ModelZone no LinkDownRight CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDownRight ]
    late [ LoopIterationCurrent ModelZone LinkDownRightSimple ][ LoopIterationCurrent CheckZone LinkDownRightSimple ] -> [ LoopIterationCurrent ModelZone LinkDownRightSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownRightSimple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkDownLeft ][ LoopIterationCurrent CheckZone no LinkDownLeft ] -> [ LoopIterationCurrent ModelZone no LinkDownLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkDownLeft ]
    late [ LoopIterationCurrent ModelZone LinkDownLeftSimple ][ LoopIterationCurrent CheckZone LinkDownLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkDownLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkDownLeftSimple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    late [ LoopIterationCurrent ModelZone no LinkUpLeft ][ LoopIterationCurrent CheckZone no LinkUpLeft ] -> [ LoopIterationCurrent ModelZone no LinkUpLeft CurrentCheckedOK ][ LoopIterationCurrent CheckZone no LinkUpLeft ]
    late [ LoopIterationCurrent ModelZone LinkUpLeftSimple ][ LoopIterationCurrent CheckZone LinkUpLeftSimple ] -> [ LoopIterationCurrent ModelZone LinkUpLeftSimple CurrentCheckedOK ][ LoopIterationCurrent CheckZone LinkUpLeftSimple ]

    late [ GlobalTagPosition ][ LoopIterationCurrent ModelZone no CurrentCheckedOK ] -> [ GlobalTagPosition GlobalTagCheckFailed ][ LoopIterationCurrent ModelZone ]
    late [ LoopIterationCurrent CurrentCheckedOK ] -> [ LoopIterationCurrent ]

    (Debug only. Comment on release)
    late [ GlobalTagPosition no GlobalTagCheckFailed ][ LoopIterationCurrent ] -> [ GlobalTagPosition ][ LoopIterationCurrent CurrentCheckedOK ]

    late [ LoopIterationCurrent ] -> [ ]
    late [ GlobalTagLoopIteration ] -> [ ]

endLoop

(TODO : win if no GlobalTagCheckFailed)

late [ GlobalTagCheckFailed] -> [ ]
late [ LoopIterationToDo] -> [ ]
late [ GlobalTagCheckZoneComplete ] -> [ ]
late [ GlobalTagMustCheckPattern ] -> [ ]


==============
WINCONDITIONS
==============



=======
LEVELS
=======

#######
#.BF..#
#...E.#
#######
#..B###
#....##
#.*..##
#...F##
#...#E#
#######


#############
#.BF..#######
#...E.#######
#############
#B..#########
#....#####..#
#.*..####.###
#....#####.##
#F..#E....###
#############


###############
#..H..........#
#.BCDABA......#
#..H..........#
#..H..........#
###############
#....D........#
###.....H.A...#
#.#....BC.....#
###.....H*....#
#.AAA.......H.#
###############



##################
#..H..........####
#.HAABCA......####
#..H..........####
##################
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################



##################
#.HHAC........####
#.HAAB........####
#.............####
##################
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################


(TODO : test with no unique atom)

(TODO : test with zone overlapping)