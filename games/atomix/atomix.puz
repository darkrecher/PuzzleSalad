title Atomix demake
author Recher
homepage recher.wordpress.com

(condition : il faut au moins un mur)
(TODO : expliquer comment faire le modèle)
(TODO : expliquer qu'il faut une marge sur les bords, pour que le rectangle de la ZoneCheck puisse avoir la place de se déplacer)

verbose_logging
again_interval 1.1

run_rules_on_level_start

========
OBJECTS
========

Background
black

BackgroundModelBlockPropagate
black

Wall
lightgray

WallSeparateFromModel
lightgray

ModelStartPropagate
red

Model
darkgray

Hydrogen
transparent gray
00000
01110
01110
01110
00000

Carbon
transparent blue
00000
01110
01110
01110
00000

Oxygen
transparent red
00000
01110
01110
01110
00000

AtomTagged
transparent

AtomCheckedOK
transparent

SelectedAtom
transparent yellow
10101
00000
10001
00000
10101

Player
transparent orange
10101
00000
10001
00000
10101

UniqueAtomStartCheck
transparent red
00000
00000
00100
00000
00000

CopyToRight
transparent red yellow
00000
00000
00210
00000
00000

CopyToUp
transparent

CopyToDown
transparent

CopyToLeft
transparent

ZoneCheckToInitiate
transparent

ZoneCheck
purple

ZoneCheckVertical
transparent

ZoneCheckHorizontal
transparent

CheckedAtomOK
pink

CheckedFail
transparent

CurrentlyChecking
red

CheckOneByOne
transparent

LinkLeftSimple
transparent #eeeeee
00000
00000
11100
00000
00000

LinkLeftDouble
transparent #eeeeee
00000
11100
00000
11100
00000

LinkLeftTriple
transparent #eeeeee
00000
11100
11100
11100
00000

LinkLeftTagged
transparent

LinkLeftCheckedOK
transparent

LinkRightSimple
transparent #eeeeee
00000
00000
00111
00000
00000

LinkRightDouble
transparent #eeeeee
00000
00111
00000
00111
00000

LinkRightTriple
transparent #eeeeee
00000
00111
00111
00111
00000

LinkRightTagged
transparent

LinkRightCheckedOK
transparent

LinkUpSimple
transparent #eeeeee
00100
00100
00100
00000
00000

LinkUpDouble
transparent #eeeeee
01010
01010
01010
00000
00000

LinkUpTriple
transparent #eeeeee
01110
01110
01110
00000
00000

LinkUpTagged
transparent

LinkUpCheckedOK
transparent

LinkDownSimple
transparent #eeeeee
00000
00000
00100
00100
00100

LinkDownDouble
transparent #eeeeee
00000
00000
01010
01010
01010

LinkDownTriple
transparent #eeeeee
00000
00000
01110
01110
01110

LinkDownTagged
transparent

LinkDownCheckedOK
transparent

LinkUpRightSimple
transparent #eeeeee
00001
00010
00100
00000
00000

LinkUpRightTagged
transparent

LinkUpRightCheckedOK
transparent

LinkDownRightSimple
transparent #eeeeee
00000
00000
00100
00010
00001

LinkDownRightTagged
transparent

LinkDownRightCheckedOK
transparent

LinkDownLeftSimple
transparent #eeeeee
00000
00000
00100
01000
10000

LinkDownLeftTagged
transparent

LinkDownLeftCheckedOK
transparent

LinkUpLeftSimple
transparent #eeeeee
10000
01000
00100
00000
00000

LinkUpLeftTagged
transparent

LinkUpLeftCheckedOK
transparent


GlobalTagPosition
transparent

GlobalTagFoundUniqueAtom
transparent

GlobalTagSearchUniqueFail
transparent

LoopIterationToDo
transparent

LoopIterationCurrent
transparent

GlobalTagLoopIteration
transparent

=======
LEGEND
=======

. = Background
, = BackgroundModelBlockPropagate
# = Wall
? = Background and ModelStartPropagate and Model and ZoneCheckToInitiate
! = WallSeparateFromModel
(TODO : auto-find a place for the GlobalTagPosition)
% = WallSeparateFromModel and GlobalTagPosition
* = Player

A = Hydrogen and LinkLeftSimple
B = Hydrogen and LinkRightSimple
C = Hydrogen and LinkUpSimple
D = Hydrogen and LinkDownSimple
H = Hydrogen
O = Oxygen
Z = Carbon and LinkLeftSimple and LinkRightSimple

Atom = Hydrogen or Carbon or Oxygen
LinkLeft = LinkLeftSimple or LinkLeftDouble or LinkLeftTriple
LinkRight = LinkRightSimple or LinkRightDouble or LinkRightTriple
LinkUp = LinkUpSimple or LinkUpDouble or LinkUpTriple
LinkDown = LinkDownSimple or LinkDownDouble or LinkDownTriple
LinkUpRight = LinkUpRightSimple
LinkDownRight = LinkDownRightSimple
LinkDownLeft = LinkDownLeftSimple
LinkUpLeft = LinkUpLeftSimple
Link = LinkLeft or LinkRight or LinkUp or LinkDown or LinkUpRight or LinkDownRight or LinkDownLeft or LinkUpLeft

Obstacle = WallSeparateFromModel or Wall or Atom

ModelBlockPropagate = Wall or BackgroundModelBlockPropagate or WallSeparateFromModel

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background

Model, BackgroundModelBlockPropagate, ModelBlockPropagate

ZoneCheckHorizontal
ZoneCheckVertical
ZoneCheck

ModelStartPropagate
CurrentlyChecking
CheckOneByOne
CheckedFail
CheckedAtomOK

LinkLeftSimple, LinkLeftDouble, LinkLeftTriple
LinkLeftTagged
LinkLeftCheckedOK
LinkRightSimple, LinkRightDouble, LinkRightTriple
LinkRightTagged
LinkRightCheckedOK
LinkUpSimple, LinkUpDouble, LinkUpTriple
LinkUpTagged
LinkUpCheckedOK
LinkDownSimple, LinkDownDouble, LinkDownTriple
LinkDownTagged
LinkDownCheckedOK

LinkUpRightSimple
LinkUpRightTagged
LinkUpRightCheckedOK
LinkDownRightSimple
LinkDownRightTagged
LinkDownRightCheckedOK
LinkDownLeftSimple
LinkDownLeftTagged
LinkDownLeftCheckedOK
LinkUpLeftSimple
LinkUpLeftTagged
LinkUpLeftCheckedOK

Wall, Hydrogen, Carbon, Oxygen
AtomTagged
AtomCheckedOK


Player, WallSeparateFromModel

SelectedAtom

UniqueAtomStartCheck

ZoneCheckToInitiate

CopyToRight
CopyToUp
CopyToDown
CopyToLeft

GlobalTagPosition
GlobalTagFoundUniqueAtom
GlobalTagLoopIteration
GlobalTagSearchUniqueFail

LoopIterationToDo
LoopIterationCurrent

======
RULES
======

( --- Model propagation --- )
(Useful only at level start)

[ ModelStartPropagate | no Model no ModelBlockPropagate ] -> [ ModelStartPropagate | Model no ModelBlockPropagate ]

[ ModelStartPropagate ] [ Model | no Model no ModelBlockPropagate ] -> [ ModelStartPropagate ] [ Model | Model no ModelBlockPropagate ]

[ ModelStartPropagate ][ Wall ] -> [ Model ][ Wall CheckOneByOne ]

( --- Search for a unique atom in the ModelZone --- )

(Initiate a loop on all the Atoms and Link of the ModelZone)
[ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ Model Atom ] -> [ GlobalTagPosition ][ Model Atom LoopIterationToDo ]
[ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ Model Link ] -> [ GlobalTagPosition ][ Model Link LoopIterationToDo ]

(Starts one loop iteration, while no unique atom is found.)
[ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagLoopIteration ][ LoopIterationToDo ] -> [ GlobalTagPosition GlobalTagLoopIteration ][ LoopIterationCurrent ]

(Tags all the atoms that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no Atom' case))
+ [ LoopIterationCurrent Hydrogen ][ Hydrogen ] -> [ LoopIterationCurrent Hydrogen ][ Hydrogen AtomTagged ]
+ [ LoopIterationCurrent Carbon ][ Carbon ] -> [ LoopIterationCurrent Carbon ][ Carbon AtomTagged ]
+ [ LoopIterationCurrent Oxygen ][ Oxygen ] -> [ LoopIterationCurrent Oxygen ][ Oxygen AtomTagged ]
+ [ LoopIterationCurrent no Atom ][ no Atom ] -> [ LoopIterationCurrent no Atom ][ no Atom AtomTagged ]

(Tags all the LinkLeft that are the same as the current one, in the ModelZone and the ArenaZone. (including the 'no LinkLeft' case))
+ [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple ] -> [ LoopIterationCurrent LinkLeftSimple ][ LinkLeftSimple LinkLeftTagged ]
+ [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble ] -> [ LoopIterationCurrent LinkLeftDouble ][ LinkLeftDouble LinkLeftTagged ]
+ [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple ] -> [ LoopIterationCurrent LinkLeftTriple ][ LinkLeftTriple LinkLeftTagged ]
+ [ LoopIterationCurrent no LinkLeft ][ no LinkLeft ] -> [ LoopIterationCurrent no LinkLeft ][ no LinkLeft LinkLeftTagged ]

(Tags all the LinkRight, etc.)
+ [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple ] -> [ LoopIterationCurrent LinkRightSimple ][ LinkRightSimple LinkRightTagged ]
+ [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble ] -> [ LoopIterationCurrent LinkRightDouble ][ LinkRightDouble LinkRightTagged ]
+ [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple ] -> [ LoopIterationCurrent LinkRightTriple ][ LinkRightTriple LinkRightTagged ]
+ [ LoopIterationCurrent no LinkRight ][ no LinkRight ] -> [ LoopIterationCurrent no LinkRight ][ no LinkRight LinkRightTagged ]

(etc.)
+ [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple ] -> [ LoopIterationCurrent LinkUpSimple ][ LinkUpSimple LinkUpTagged ]
+ [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble ] -> [ LoopIterationCurrent LinkUpDouble ][ LinkUpDouble LinkUpTagged ]
+ [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple ] -> [ LoopIterationCurrent LinkUpTriple ][ LinkUpTriple LinkUpTagged ]
+ [ LoopIterationCurrent no LinkUp ][ no LinkUp ] -> [ LoopIterationCurrent no LinkUp ][ no LinkUp LinkUpTagged ]

(etc.)
+ [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple ] -> [ LoopIterationCurrent LinkDownSimple ][ LinkDownSimple LinkDownTagged ]
+ [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble ] -> [ LoopIterationCurrent LinkDownDouble ][ LinkDownDouble LinkDownTagged ]
+ [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple ] -> [ LoopIterationCurrent LinkDownTriple ][ LinkDownTriple LinkDownTagged ]
+ [ LoopIterationCurrent no LinkDown ][ no LinkDown ] -> [ LoopIterationCurrent no LinkDown ][ no LinkDown LinkDownTagged ]

(If another Atom in the ModelZone has all its characteristics tagged, it means the current Atom is not Unique.)
(This loop iteration is a fail.)
+ [ GlobalTagPosition no GlobalTagFoundUniqueAtom ][ no LoopIterationCurrent Model AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged ] -> [ GlobalTagPosition no GlobalTagFoundUniqueAtom GlobalTagSearchUniqueFail ][ Model AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged ]

(If this loop iteration is not a fail, declares the unique atoms.)
(The following rule is executed twice : once in the ModelZone, once in the ArenaZone.)
(So that the position of the unique atom is known in both zones.)
+ [ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ][ AtomTagged LinkLeftTagged LinkRightTagged LinkUpTagged LinkDownTagged ] -> [ GlobalTagPosition ][ UniqueAtomStartCheck ]

(Declares that a unique atom has been found.)
+ [ GlobalTagPosition no GlobalTagFoundUniqueAtom no GlobalTagSearchUniqueFail ] -> [ GlobalTagPosition GlobalTagFoundUniqueAtom ]

(End of loop iteration. Cleans all tags, loop objects and fail tag.)
+ [ AtomTagged ] -> [ ]
+ [ LinkLeftTagged ] -> [ ]
+ [ LinkRightTagged ] -> [ ]
+ [ LinkUpTagged ] -> [ ]
+ [ LinkDownTagged ] -> [ ]
+ [ LoopIterationCurrent ] -> [ ]
+ [ GlobalTagLoopIteration ] -> [ ]
+ [ GlobalTagSearchUniqueFail ] -> [ ]


(not here ?)

[ CheckedAtomOK ] -> [ ]


( --- Atom grabbing by Player --- )

[ > Player SelectedAtom Atom ] -> [ > Player > SelectedAtom > Atom ]
[ > Player > SelectedAtom Link ] -> [ > Player > SelectedAtom > Link ]
[ > Player SelectedAtom UniqueAtomStartCheck ] -> [ > Player > SelectedAtom > UniqueAtomStartCheck ]
[ > UniqueAtomStartCheck ][ZoneCheck] -> [ > UniqueAtomStartCheck ][ > ZoneCheck]

[ > Atom | Obstacle ] -> cancel

[ Action Player SelectedAtom ] -> [ Player no SelectedAtom ]
[ Action Player Atom no SelectedAtom ] -> [ Player Atom SelectedAtom ]


( --- ZoneCheck initialisation --- )

(test pour être sûr qu'il prend en premier le truc en haut à gauche)
([ Oxygen ] [ Model ] -> [ ] [ Model Carbon ])

[ ZoneCheckToInitiate ][ UniqueAtomStartCheck ] -> [ ZoneCheckToInitiate ][ UniqueAtomStartCheck CopyToRight CopyToUp CopyToDown CopyToLeft ]
[ ZoneCheckToInitiate ][ UniqueAtomStartCheck no Model ] -> [ ZoneCheckToInitiate ][ UniqueAtomStartCheck no Model ZoneCheck ]

RIGHT [ UniqueAtomStartCheck Model CopyToRight | Model ] [ UniqueAtomStartCheck no Model CopyToRight |  ] -> [ UniqueAtomStartCheck Model | CopyToRight Model ] [ UniqueAtomStartCheck no Model | CopyToRight ZoneCheck ZoneCheckVertical ]

RIGHT [ UniqueAtomStartCheck Model | ... | CopyToRight | Model ] [ UniqueAtomStartCheck no Model | ... | CopyToRight |  ] -> [ UniqueAtomStartCheck Model | ... |  | CopyToRight Model ] [ UniqueAtomStartCheck no Model | ... |  | CopyToRight ZoneCheck ZoneCheckVertical ]

LEFT [ UniqueAtomStartCheck Model CopyToLeft | Model ] [ UniqueAtomStartCheck no Model CopyToLeft |  ] -> [ UniqueAtomStartCheck Model | CopyToLeft Model ] [ UniqueAtomStartCheck no Model | CopyToLeft ZoneCheck ZoneCheckVertical ]

LEFT [ UniqueAtomStartCheck Model | ... | CopyToLeft | Model ] [ UniqueAtomStartCheck no Model | ... | CopyToLeft |  ] -> [ UniqueAtomStartCheck Model | ... |  | CopyToLeft Model ] [ UniqueAtomStartCheck no Model | ... |  | CopyToLeft ZoneCheck ZoneCheckVertical ]

UP [ UniqueAtomStartCheck Model CopyToUp | Model ] [ UniqueAtomStartCheck no Model CopyToUp |  ] -> [ UniqueAtomStartCheck Model | CopyToUp Model ] [ UniqueAtomStartCheck no Model | CopyToUp ZoneCheck ZoneCheckHorizontal ]

UP [ UniqueAtomStartCheck Model | ... | CopyToUp | Model ] [ UniqueAtomStartCheck no Model | ... | CopyToUp |  ] -> [ UniqueAtomStartCheck Model | ... |  | CopyToUp Model ] [ UniqueAtomStartCheck no Model | ... |  | CopyToUp ZoneCheck ZoneCheckHorizontal ]

DOWN [ UniqueAtomStartCheck Model CopyToDown | Model ] [ UniqueAtomStartCheck no Model CopyToDown |  ] -> [ UniqueAtomStartCheck Model | CopyToDown Model ] [ UniqueAtomStartCheck no Model | CopyToDown ZoneCheck ZoneCheckHorizontal ]

DOWN [ UniqueAtomStartCheck Model | ... | CopyToDown | Model ] [ UniqueAtomStartCheck no Model | ... | CopyToDown |  ] -> [ UniqueAtomStartCheck Model | ... |  | CopyToDown Model ] [ UniqueAtomStartCheck no Model | ... |  | CopyToDown ZoneCheck ZoneCheckHorizontal ]

[ ZoneCheckToInitiate ] -> [ ]
[ CopyToUp ] -> [ ]
[ CopyToRight ] -> [ ]
[ CopyToDown ] -> [ ]
[ CopyToLeft ] -> [ ]

VERTICAL [ ZoneCheckVertical | ... | no ZoneCheckVertical ] -> [ ZoneCheckVertical | ... | ZoneCheckVertical ]
HORIZONTAL [ ZoneCheckHorizontal | ... | no ZoneCheckHorizontal ] -> [ ZoneCheckHorizontal | ... | ZoneCheckHorizontal ]

[ ZoneCheckVertical ZoneCheckHorizontal ] -> [ ZoneCheck ]
[ ZoneCheckVertical ] -> [ ]
[ ZoneCheckHorizontal ] -> [ ]


( --- Check equality between Model and ZoneCheck --- )

(PuzzleScript, why you no have variables ?)
late [ CheckOneByOne no CurrentlyChecking ][ Model no CheckedAtomOK no CheckedFail ][ ZoneCheck no CheckedAtomOK no CheckedFail ] -> [ CheckOneByOne CurrentlyChecking ][ Model CurrentlyChecking no CheckedAtomOK no CheckedFail ][ ZoneCheck CurrentlyChecking no CheckedAtomOK no CheckedFail ]
+ late [ Model CurrentlyChecking no Atom ][ ZoneCheck CurrentlyChecking no Atom ] -> [ Model CurrentlyChecking no Atom CheckedAtomOK ][ ZoneCheck CurrentlyChecking no Atom CheckedAtomOK ]
+ late [ Model CurrentlyChecking Carbon ][ ZoneCheck CurrentlyChecking Carbon ] -> [ Model CurrentlyChecking Carbon CheckedAtomOK ][ ZoneCheck CurrentlyChecking Carbon CheckedAtomOK ]
+ late [ Model CurrentlyChecking Hydrogen ][ ZoneCheck CurrentlyChecking Hydrogen ] -> [ Model CurrentlyChecking Hydrogen CheckedAtomOK ][ ZoneCheck CurrentlyChecking Hydrogen CheckedAtomOK ]
+ late [ Model CurrentlyChecking no CheckedAtomOK ][ ZoneCheck CurrentlyChecking no CheckedAtomOK ] -> [ Model CurrentlyChecking CheckedFail ][ ZoneCheck CurrentlyChecking CheckedFail ]
+ late [ CurrentlyChecking ] -> [ ]

late [ CheckedFail] -> [ ]

==============
WINCONDITIONS
==============



=======
LEVELS
=======

##################
#,?H....,.....####
#,BCDABA,.....####
#,.H....,.....####
%!!!!!!!!!!!!!!!!!
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################



##################
#,?H....,.....####
#,HAABCA,.....####
#,.H....,.....####
%!!!!!!!!!!!!!!!!!
#.............####
#.......H.A...####
#......BC.....####
#.......H*....####
#.AAA.........####
##################
